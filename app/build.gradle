apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

/**
 * Tag and commit count. Use these to automate versioning.
 * Need to set at least 375, cause the Play Store version currently 374
 */
def gitCommitCount = 374 + Integer.parseInt('git rev-list --count --remotes'.execute([], project.rootDir).text.trim())

ext.versionMajor = 2
ext.versionMinor = 1
ext.versionPatch = 0
ext.versionClassifier = 'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir).text.trim()

private String generateVersionName() {
    return "${versionMajor}.${ext.versionMinor}.${ext.versionPatch}" + "-" + ext.versionClassifier
}

ext {
    LIBRARY_SUPPORT = '27.1.1'
}

android {
    compileSdkVersion 27
    buildToolsVersion "28.0.2"
    defaultConfig {
        applicationId "hu.sherad.hos"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode gitCommitCount
        versionName generateVersionName()
        multiDexEnabled true
    }

    buildTypes {
        print("Git commit count: " + gitCommitCount + "\n")
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            ext.enableCrashlytics = false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            details.useVersion "$LIBRARY_SUPPORT"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:design:$LIBRARY_SUPPORT"
    implementation "com.android.support:recyclerview-v7:$LIBRARY_SUPPORT"
    implementation "com.android.support:palette-v7:$LIBRARY_SUPPORT"
    implementation "com.android.support:customtabs:$LIBRARY_SUPPORT"
    implementation "com.android.support:cardview-v7:$LIBRARY_SUPPORT"
    // CONSTRAINT LAYOUT https://developer.android.com/training/constraint-layout/index.html
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    // GLIDE https://github.com/bumptech/glide
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'
    // MATERIAL DIALOG https://github.com/afollestad/material-dialogs
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    // MATERIAL DIMENS https://github.com/DmitryMalkovich/material-design-dimens
    implementation 'com.dmitrymalkovich.android:material-design-dimens:1.4'
    // VOLLEY https://developer.android.com/training/volley/index.html
    implementation 'com.android.volley:volley:1.0.0'
    // FABRIC https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
    implementation 'com.google.firebase:firebase-core:16.0.3'
    implementation 'com.google.firebase:firebase-config:16.0.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.3@aar') {
        transitive = true
    }
    // JSOUP LIBRARY https://jsoup.org/download
    implementation 'org.jsoup:jsoup:1.11.3'
    // PHOTO VIEW https://github.com/chrisbanes/PhotoView
    implementation 'com.github.chrisbanes:PhotoView:2.1.4'
    // Google GSON https://github.com/google/gson
    implementation 'com.google.code.gson:gson:2.8.5'
    // Android GIF Drawable https://github.com/koral--/android-gif-drawable
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.12'
    // MaterialRangeBar https://github.com/oli107/material-range-bar
    implementation 'com.appyvet:materialrangebar:1.4.2'
    // TapTargetView https://github.com/KeepSafe/TapTargetView
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.11.0'
    // AES Crypt https://github.com/scottyab/AESCrypt-Android
    implementation 'com.scottyab:aescrypt:0.0.1'
    implementation 'android.arch.navigation:navigation-fragment:1.0.0-alpha05'

}
apply plugin: 'com.google.gms.google-services'